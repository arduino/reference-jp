---
title: shiftOut()
categories: [ "Functions" ]
subCategories: [ "応用入出力" ]
---





= shiftOut()


// OVERVIEW SECTION STARTS
[#overview]
--

[float]
=== 説明
1バイトのデータを1ビットだけシフトアウトします. 最上位ビット(MSB，左側)と最下位ビット(LSB，右側)のどちらからでも実行できます. 各ビットはデータピンに順番に書き込まれたのち，クロックピンにパルスが出力されることで(HIGHののちにLOW)，ビットが使用可能であることを示します．

注- もし，立ち上がりエッジをクロックとして使用するデバイスを接続する場合は，`shiftOut()` を実行する前に，最初にクロックピンをLOWにしておく必要があります．たとえば， `digitalWrite(clockPin, LOW)` などを実行します．

この関数はソフトウェアによって実行されています; ハードウェアで実行され，高速で動作する link:../SPI[SPI library] も参照(特定のピンでしか動作しません)．
[%hardbreaks]


[float]
=== 構文
`shiftOut(dataPin, clockPin, bitOrder, value)`


[float]
=== パラメータ
`dataPin`: 各ビットを出力するピン (int)

`clockPin`: データピンに正しい値がセットされたら，パルスを出力するピン (int)

`bitOrder`: シフトする順番; MSBFIRST (最上位ビット側から) または LSBFIRST(最下位ビット側から)

`value`: シフトアウトするデータ (byte)

[float]
=== 戻り値
なし

--
// OVERVIEW SECTION ENDS




// HOW TO USE SECTION STARTS
[#howtouse]
--

[float]
=== コード例
// Describe what the example code is all about and add relevant code   ►►►►► THIS SECTION IS MANDATORY ◄◄◄◄◄
回路については，こちらをご覧ください http://arduino.cc/en/Tutorial/ShiftOut[tutorial on controlling a 74HC595 shift register].

[source,arduino]
----
//**************************************************************//
//  Name    : shiftOutCode, Hello World                         //
//  Author  : Carlyn Maw,Tom Igoe                               //
//  Date    : 25 Oct, 2006                                      //
//  Version : 1.0                                               //
//  Notes   : Code for using a 74HC595 Shift Register           //
//          : to count from 0 to 255                            //
//****************************************************************

//74HC595のST_CPに接続するピン
int latchPin = 8;
//74HC595のSH_CPに接続するピン
int clockPin = 12;
//74HC595のDSに接続するピン
int dataPin = 11;

void setup() {
  //メインループで使用されているピンを出力ピンに設定する
  pinMode(latchPin, OUTPUT);
  pinMode(clockPin, OUTPUT);
  pinMode(dataPin, OUTPUT);
}

void loop() {
  //カウントアップのルーチン
  for (int j = 0; j < 256; j++) {
    //通信中は，latchPinをLOWにする
    digitalWrite(latchPin, LOW);
    shiftOut(dataPin, clockPin, LSBFIRST, j);
    //通信が終了したらlatchPinをHIGHにする
    digitalWrite(latchPin, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]

[float]
=== 注意
データピンとクロックピンは，link:../digital-io/pinmode/[pinMode()] を使用して，出力(OUTPUT)に設定する必要があります．.

shiftOutは1バイト(8ビット)を出力するために作られています．よって，255より大きな値を出力するためには，2段階の操作が必要となります．
[source,arduino]
----
//MSBFIRSTのシリアルで使用
int data = 500;
//上位バイトをシフトアウト
shiftOut(dataPin, clock, MSBFIRST, (data >> 8));
//下位バイトをシフトアウト
shiftOut(dataPin, clock, MSBFIRST, data);

//LSBFIRSTのシリアルで使用
data = 500;
//下位バイトをシフトアウト
shiftOut(dataPin, clock, LSBFIRST, data);
//上位バイトをシフトアウト
shiftOut(dataPin, clock, LSBFIRST, (data >> 8));
----
[%hardbreaks]

--
// HOW TO USE SECTION ENDS
